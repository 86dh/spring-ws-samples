configurations {
	xmlbeans
}

ext.springWsVersion =  	  '2.1.4.RELEASE'

task xmlbeans {
	ext.schema = "${projectDir}/../../server/src/main/webapp/messages.xsd"
    ext.classesDir = "${buildDir}/classes/xmlbeans"

    inputs.files schema
    outputs.dir classesDir

    doLast() {
            project.ant {
                    taskdef name: "xmlbeans",
                                    classname: "org.apache.xmlbeans.impl.tool.XMLBean",
                                    classpath: configurations.xmlbeans.asPath

                    xmlbeans(classgendir: classesDir, schema: schema,
                                    compiler: "modern", verbose: "false",
                                    classpath: configurations.xmlbeans.asPath)
            }
    }
}

dependencies {
	compile(files(xmlbeans.classesDir).builtBy(xmlbeans))
	compile("org.apache.xmlbeans:xmlbeans:2.4.0")
	compile("org.springframework.ws:spring-ws-core:$springWsVersion")
	runtime("org.springframework.ws:spring-ws-security:$springWsVersion") {
        exclude group: 'com.sun.xml.wsit', module: 'wsit-rt'
        exclude group: 'com.sun.xml.wsit', module: 'xws-security'
	}	
	runtime("com.sun.xml.wss:xws-security:3.0") {
        exclude group: 'javax.xml.crypto', module: 'xmldsig'
    }
	runtime("log4j:log4j:1.2.16")
	testCompile("org.springframework.ws:spring-ws-test:$springWsVersion")
	xmlbeans("org.apache.xmlbeans:xmlbeans:2.4.0")
}

task runClient(dependsOn: 'classes', type:JavaExec) {
	main = "org.springframework.ws.samples.airline.client.sws.Driver"
	classpath = sourceSets.main.runtimeClasspath
}