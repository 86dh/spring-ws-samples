configurations {
	jaxb
}

task genJaxb {
	ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
	ext.classesDir = "${buildDir}/classes/jaxb"
	ext.schema = "${projectDir}/../../server/src/main/resources/schema.xsd"

	inputs.files schema
	outputs.dir classesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)
			mkdir(dir: classesDir)

			xjc(destdir: sourcesDir, schema: schema,
					package: "org.springframework.ws.samples.mtom.client.sws") {
				produces(dir: sourcesDir, includes: "**/*.java")
			}

			javac(destdir: classesDir, source: 1.6, target: 1.6, debug: true,
					debugLevel: "lines,vars,source",
					classpath: configurations.jaxb.asPath) {
				src(path: sourcesDir)
				include(name: "**/*.java")
				include(name: "*.java")
			}

			copy(todir: classesDir) {
				fileset(dir: sourcesDir, erroronmissingdir: false) {
					exclude(name: "**/*.java")
				}
			}
		}
	}
}

dependencies {
	compile("org.springframework.ws:spring-ws-core:$springWsVersion")
    compile("org.springframework:spring-context:$springVersion")
    compile("org.apache.ws.commons.axiom:axiom-api:1.2.14")
	compile(files(genJaxb.classesDir).builtBy(genJaxb))

	runtime("log4j:log4j:1.2.16")
    runtime("org.apache.ws.commons.axiom:axiom-impl:1.2.14")

	jaxb "com.sun.xml.bind:jaxb-xjc:2.1.7"	
}

task runClient(dependsOn: 'classes', type:JavaExec) {
	main = "org.springframework.ws.samples.mtom.client.sws.Driver"
	classpath = sourceSets.main.runtimeClasspath
    systemProperty("java.awt.headless", "true")
}