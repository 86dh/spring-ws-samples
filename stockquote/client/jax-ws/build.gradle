configurations {
	wsimport
}

ext.springWsVersion =  	  '2.1.4.RELEASE'

task wsImport {
	ext.outputDir = "${buildDir}/classes/wsimport"
	ext.wsdl = "${projectDir}/../../server//src/main/resources/org/springframework/ws/samples/stockquote/ws/stockquote.wsdl"

	inputs.files wsdl
	outputs.dir outputDir

	doLast() {
		project.ant {
			taskdef name: "wsimport", classname: "com.sun.tools.ws.ant.WsImport",
					classpath: configurations.wsimport.asPath
			mkdir(dir: outputDir)

			wsimport(destdir: outputDir, wsdl: wsdl,
					package: "org.springframework.ws.samples.stockquote.client.jaxws") {
				produces(dir: outputDir, includes: "**/*.class")
			}
		}
	}
}

dependencies {
	compile("org.springframework.ws:spring-ws-core:$springWsVersion")
	compile(files(wsImport.outputDir).builtBy(wsImport))
	runtime("log4j:log4j:1.2.16")
	wsimport "com.sun.xml.ws:jaxws-tools:2.1.7"	
}

task runClient(dependsOn: 'classes', type:JavaExec) {
	main = "org.springframework.ws.samples.stockquote.client.jaxws.Main"
	classpath = sourceSets.main.runtimeClasspath
}